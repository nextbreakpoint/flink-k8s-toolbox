plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group = 'com.nextbreakpoint'
version = '1.1.10-beta'
description = 'Tool for managing Flink clusters on Kubernetes'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

dependencies {
    constraints {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.41"
        implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.41"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2"
        implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.11.2"
        implementation "org.apache.logging.log4j:log4j-1.2-api:2.11.2"
        implementation "org.apache.logging.log4j:log4j-jcl:2.11.2"
        implementation "org.apache.logging.log4j:log4j-jul:2.11.2"
        implementation "io.kubernetes:client-java:5.0.0"
        implementation "com.google.code.gson:gson:2.8.5"
        implementation "io.vertx:vertx-core:3.8.0"
        implementation "io.vertx:vertx-rx-java:3.8.0"
        implementation "io.vertx:vertx-web:3.8.0"
        implementation "io.vertx:vertx-web-common:3.8.0"
        implementation "io.vertx:vertx-web-client:3.8.0"
        implementation "io.vertx:vertx-micrometer-metrics:3.8.0"
        implementation "com.github.ajalt:clikt:2.1.0"
        implementation "javax.xml.bind:jaxb-api:2.2.11"
        implementation "javax.activation:activation:1.1.1"
        implementation "com.fasterxml.jackson.core:jackson-annotations:2.9.9"
        implementation "com.fasterxml.jackson.core:jackson-databind:2.9.9.3"
        implementation "io.micrometer:micrometer-registry-prometheus:1.1.5"
        implementation "com.nextbreakpoint:com.nextbreakpoint.flinkclient:1.0.1"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.3.41"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:5.5.1"
        testImplementation "org.junit.platform:junit-platform-runner:1.5.1"
        testImplementation "org.assertj:assertj-core:3.12.2"
        testImplementation "org.mockito:mockito-core:3.0.0"
        testImplementation "com.github.tomakehurst:wiremock:2.24.0"
        testImplementation "org.awaitility:awaitility:3.1.6"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl"
    implementation "org.apache.logging.log4j:log4j-1.2-api"
    implementation "org.apache.logging.log4j:log4j-jcl"
    implementation "org.apache.logging.log4j:log4j-jul"
    implementation "io.kubernetes:client-java"
    implementation "com.google.code.gson:gson"
    implementation "io.vertx:vertx-core"
    implementation "io.vertx:vertx-rx-java"
    implementation "io.vertx:vertx-web"
    implementation "io.vertx:vertx-web-common"
    implementation "io.vertx:vertx-web-client"
    implementation "io.vertx:vertx-micrometer-metrics"
    implementation "com.github.ajalt:clikt"
    implementation "javax.xml.bind:jaxb-api"
    implementation "javax.activation:activation"
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "com.nextbreakpoint:com.nextbreakpoint.flinkclient"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.platform:junit-platform-runner"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
    testImplementation "com.github.tomakehurst:wiremock"
    testImplementation "org.awaitility:awaitility"
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}

compileKotlin {
    kotlinOptions.suppressWarnings = true
    kotlinOptions.jvmTarget = '1.8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName = "flink-k8s-toolbox"
    archiveClassifier = "with-dependencies"
    manifest {
        attributes 'Main-Class': 'com.nextbreakpoint.flinkoperator.cli.FlinkK8SToolboxMain', \
                   'Implementation-Title': project.name, \
                   'Implementation-Version': project.version, \
                   'Built-By': System.getProperty('user.name'), \
                   'Built-JDK': System.getProperty('java.version'), \
                   'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from (sourceSets.main.resources) {
        include '*.properties'
        filter(ReplaceTokens, tokens: ['name': project.name, 'version': project.version])
    }
}