plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group = 'com.nextbreakpoint'
version = '1.2.2-beta'
description = 'Tool for managing Flink clusters on Kubernetes'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

dependencies {
    constraints {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.50"
        implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.50"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2"
        implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.12.1"
        implementation "org.apache.logging.log4j:log4j-1.2-api:2.12.1"
        implementation "org.apache.logging.log4j:log4j-jcl:2.12.1"
        implementation "org.apache.logging.log4j:log4j-jul:2.12.1"
        implementation "io.kubernetes:client-java:6.0.1"
        implementation "com.google.code.gson:gson:2.8.6"
        implementation "io.vertx:vertx-core:3.8.3"
        implementation "io.vertx:vertx-rx-java:3.8.3"
        implementation "io.vertx:vertx-web:3.8.3"
        implementation "io.vertx:vertx-web-common:3.8.3"
        implementation "io.vertx:vertx-web-client:3.8.3"
        implementation "io.vertx:vertx-micrometer-metrics:3.8.3"
        implementation "com.github.ajalt:clikt:2.2.0"
        implementation "javax.xml.bind:jaxb-api:2.2.11"
        implementation "javax.activation:activation:1.1.1"
        implementation "com.fasterxml.jackson.core:jackson-annotations:2.10.0"
        implementation "com.fasterxml.jackson.core:jackson-databind:2.10.0"
        implementation "io.micrometer:micrometer-registry-prometheus:1.1.5"
        implementation "com.nextbreakpoint:com.nextbreakpoint.flinkclient:1.0.1"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.3.41"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:5.5.2"
        testImplementation "org.junit.platform:junit-platform-runner:1.5.2"
        testImplementation "org.assertj:assertj-core:3.13.2"
        testImplementation "org.mockito:mockito-core:3.1.0"
        testImplementation "com.github.tomakehurst:wiremock-jre8:2.25.1"
        testImplementation "org.awaitility:awaitility:4.0.1"
        testImplementation 'com.fasterxml.jackson.core:jackson-core:2.10.0'
        testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
        testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.0'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl"
    implementation "org.apache.logging.log4j:log4j-1.2-api"
    implementation "org.apache.logging.log4j:log4j-jcl"
    implementation "org.apache.logging.log4j:log4j-jul"
    implementation "io.kubernetes:client-java"
    implementation "com.google.code.gson:gson"
    implementation "io.vertx:vertx-core"
    implementation "io.vertx:vertx-rx-java"
    implementation "io.vertx:vertx-web"
    implementation "io.vertx:vertx-web-common"
    implementation "io.vertx:vertx-web-client"
    implementation "io.vertx:vertx-micrometer-metrics"
    implementation "com.github.ajalt:clikt"
    implementation "javax.xml.bind:jaxb-api"
    implementation "javax.activation:activation"
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "com.nextbreakpoint:com.nextbreakpoint.flinkclient"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.platform:junit-platform-runner"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
    testImplementation "com.github.tomakehurst:wiremock-jre8"
    testImplementation "org.awaitility:awaitility"
    testImplementation 'com.fasterxml.jackson.core:jackson-core'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}

compileKotlin {
    kotlinOptions.suppressWarnings = true
    kotlinOptions.jvmTarget = '1.8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform() {
        excludeTags 'IntegrationTest'
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'IntegrationTest'
    }
//    check.dependsOn it
//    shouldRunAfter test
}

shadowJar {
    archiveBaseName = "flink-k8s-toolbox"
    archiveClassifier = "with-dependencies"
    manifest {
        attributes 'Main-Class': 'com.nextbreakpoint.flinkoperator.cli.Main', \
                   'Implementation-Title': project.name, \
                   'Implementation-Version': project.version, \
                   'Built-By': System.getProperty('user.name'), \
                   'Built-JDK': System.getProperty('java.version'), \
                   'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from (sourceSets.main.resources) {
        include '*.properties'
        filter(ReplaceTokens, tokens: ['name': project.name, 'version': project.version])
    }
}